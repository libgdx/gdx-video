import com.diffplug.spotless.LineEnding

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        jcenter()
    }
    dependencies {
        classpath "org.wisepersist:gwt-gradle-plugin:$gwtPluginVersion"
        classpath "com.android.tools.build:gradle:$androidPluginVersion"
        classpath "com.mobidevelop.robovm:robovm-gradle-plugin:$robovmVersion"
        classpath "com.badlogicgames.gdx:gdx-jnigen-gradle:2.0.0-SNAPSHOT"
    }
}

plugins {
    id "com.diffplug.spotless" version "5.3.0"
}

ext {
    projectGroup = "gdx-video"
}

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
}

subprojects {
    apply plugin: "com.diffplug.spotless"

    spotless {
        lineEndings LineEnding.UNIX

        java {
            target 'src/**/*.java'
            licenseHeader '/*******************************************************************************\n' +
                    ' * Copyright 2014 See AUTHORS file.\n' +
                    ' *\n' +
                    ' * Licensed under the Apache License, Version 2.0 (the "License");\n' +
                    ' * you may not use this file except in compliance with the License.\n' +
                    ' * You may obtain a copy of the License at\n' +
                    ' *\n' +
                    ' *   http://www.apache.org/licenses/LICENSE-2.0\n' +
                    ' *\n' +
                    ' * Unless required by applicable law or agreed to in writing, software\n' +
                    ' * distributed under the License is distributed on an "AS IS" BASIS,\n' +
                    ' * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.\n' +
                    ' * See the License for the specific language governing permissions and\n' +
                    ' * limitations under the License.\n' +
                    ' ******************************************************************************/\n'
            removeUnusedImports()
            eclipse().configFile new File(rootProject.projectDir.absolutePath, 'eclipse-formatter.xml')
        }

        cpp {
            target 'src/jni/**'
            indentWithSpaces()
        }
    }
}

tasks.eclipse.doLast {
    delete ".project"
}
