apply plugin: 'maven-publish'
apply plugin: 'signing'

afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = archivesBaseName ?: project.name

                pom {
                    name.set(POM_NAME)
                    description.set(POM_DESCRIPTION)
                    url.set(POM_URL)

                    scm {
                        url.set(POM_SCM_URL)
                        connection.set(POM_SCM_CONNECTION)
                        developerConnection.set(POM_SCM_DEV_CONNECTION)
                    }

                    licenses {
                        license {
                            name.set(POM_LICENCE_NAME)
                            url.set(POM_LICENCE_URL)
                            distribution.set(POM_LICENCE_DIST)
                        }
                    }

                    developers {
                        developer {
                            id.set('SimonIT')
                            name.set('Simon Bullik')
                            email.set('simonit.orig@gmail.com')
                        }
                    }
                }

                from components.java
            }
        }

        repositories {
            maven {
                url = version.endsWith('SNAPSHOT') ? getSnapshotRepositoryUrl() : getReleaseRepositoryUrl()

                if (getRepositoryUsername() || getRepositoryPassword()) {
                    credentials {
                        username = getRepositoryUsername()
                        password = getRepositoryPassword()
                    }
                }
            }
        }
    }

    signing {
        if (isReleaseBuild()) {
            useGpgCmd()
        }
        required { isReleaseBuild() }
        sign publishing.publications.mavenJava
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}