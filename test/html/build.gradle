buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath 'org.gretty:gretty:3.0.7'
	}
}

apply plugin: "gwt"
apply plugin: "war"
apply plugin: "org.gretty"

gwt {
	gwtVersion = "$gwtFrameworkVersion" // Should match the version used for building the GWT backend. See gradle.properties.
	maxHeapSize = '1G' // Default 256m is not enough for the GWT compiler. GWT is HUNGRY.
	minHeapSize = '1G'

	src = files(file('src/main/java')) // Needs to be in front of "modules" below.
	modules 'com.badlogic.gdx.video.test.GdxDefinition'
	devModules 'com.badlogic.gdx.video.test.GdxDefinitionSuperdev'
	project.webAppDirName = 'webapp'

	compiler.strict = true
	compiler.disableCastChecking = true
}

dependencies {
	implementation project(':test:core')
	implementation project(':gdx-video-gwt')
	implementation "com.badlogicgames.gdx:gdx:$gdxVersion:sources"
	implementation "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion"
	implementation "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion:sources"
}

import org.akhikhl.gretty.AppBeforeIntegrationTestTask
import org.wisepersist.gradle.plugins.gwt.GwtSuperDev

gretty.httpPort = 8080
gretty.resourceBase = project.buildDir.path + "/gwt/draftOut"
gretty.contextPath = "/"
gretty.portPropertiesFileName = "TEMP_PORTS.properties"

tasks.register('startHttpServer') {
	dependsOn 'draftCompileGwt'
	doFirst {
		copy {
			from "webapp"
			into gretty.resourceBase
		}
		copy {
			from "war"
			into gretty.resourceBase
		}
	}
}

task beforeRun (type: AppBeforeIntegrationTestTask) {
	dependsOn 'startHttpServer'
	// The next line allows ports to be reused instead of
	// needing a process to be manually terminated.
	file("build/TEMP_PORTS.properties").delete()
	// Somewhat of a hack; uses Gretty's support for wrapping a task in
	// a start and then stop of a Jetty server that serves files while
	// also running the SuperDev code server.
	integrationTestTask 'superDev'

	interactive false
}

tasks.register('superDev', GwtSuperDev) {
	dependsOn 'startHttpServer'
	doFirst {
		gwt.modules = gwt.devModules
	}
}
tasks.register('dist') {
	dependsOn 'clean', 'compileGwt'
	doLast {
		file("build/dist").mkdirs()
		copy {
			from "build/gwt/out"
			into "build/dist"
		}
		copy {
			from "webapp"
			into "build/dist"
		}
		copy {
			from "war"
			into "build/dist"
		}
	}
}

tasks.register('addSource') {
	doLast {
		sourceSets.main.compileClasspath += files(project(':gdx-video-core').sourceSets.main.allJava.srcDirs)
		sourceSets.main.compileClasspath += files(project(':gdx-video-gwt').sourceSets.main.allJava.srcDirs)
		sourceSets.main.compileClasspath += files(project(':gdx-video-gwt').sourceSets.main.resources.srcDirs)
		sourceSets.main.compileClasspath += files(project(':test:core').sourceSets.main.allJava.srcDirs)
	}
}

tasks.compileGwt.dependsOn(addSource)
tasks.draftCompileGwt.dependsOn(addSource)

sourceCompatibility = 8.0
sourceSets.main.java.srcDirs = [ "src/main/java/" ]

eclipse.project.name = appName + "-html"
